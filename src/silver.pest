WHITESPACE = {" " | "\t" | NEWLINE }
COMMENT = _{"//"~ (!"\n" ~ ANY)* | "/*"~ (!"*/"~ANY)*~"*/" }
char = { 'a'..'z' | 'A'..'Z' | "$" | "_"}
num = { '0'..'9' }

integer = @{num+}

// Identifiers
ident = @{ (char ) ~ (char | num | "'" )* }

// Types
// type ::=
// "Int" | "Bool" | "Perm" | "Ref" | "Rational" | // primitive types
// "Seq" "[" type "]" |                           // sequence type
// "Set" "[" type "]" |                           // set type
// "Multiset" "[" type "]" |                      // multiset type
// ident [ "[" type^,* "]" ]                      // [instance of a generic] domain type

type = { "Int" | "Bool" | "Perm" | "Ref" | "Rational"
       | "Seq" ~ "[" ~ type ~ "]"
       | "Set" ~ "[" ~ type ~ "]"
       | "Multiset" ~ "[" ~ type ~ "]"
       | ident ~ ("[" ~ type ~ ("," ~ type)* ~ "]")? // ???? domain type
     }

// predicate-access ::=
//   ident "(" exp^,* ")"         // predicate access
predicate_access = { ident ~ "(" ~ exp ~ ("," ~ exp)* ~ ")" }

// field-access ::=
//   exp "." ident |              // field access
field_access = { exp_atom~("."~ident)+ }
// loc-access ::=
//   field-access | predicate-access
loc_access = {field_access | predicate_access }
// acc-exp ::=
//    "acc" "(" loc-access ["," exp ]")" //access
                                // default is write
acc_exp = { "acc"~"("~loc_access~(","~exp)?~")" | predicate_access }
// trigger ::=
//   "{" exp^,* "}"  // a trigger for a quantification
trigger = {
       "{"~exp~(","~exp)*~"}"
}
// seq-op-exp ::=
//   exp "[" exp "]"          |  // sequence lookup
//   exp "[" ".." exp "]"     |  // take n first elements
//   exp "[" exp ".." "]"     |  // drop n first elements
//   exp "[" exp ".." exp "]" |  // take and drop
//   exp "[" exp ":=" exp "]" |  // update sequence at
seq_op_exp = {
       exp ~ "[" ~ exp ~ "]" |
       exp ~ "[" ~ ".." ~ exp ~ "]" |
       exp ~ "[" ~ exp ~ ".." ~ "]" |
       exp ~ "[" ~ exp ~ ".." ~ exp ~ "]" |
       exp ~ "[" ~ exp ~ ":=" ~ exp ~ "]"
}

// set-constructor-exp ::=
//   "Set" "[" type "]" "(" ")"             | // empty set
//   "Set" "(" exp^,* ")"                   | // explicit set
//   "Multiset" "[" type "]" "(" ")"        | // empty multiset
//   "Multiset" "(" exp^,* ")"              | // explicit multiset
set_constructor_exp = {
       "Set" ~ "[" ~ type ~ "]" ~ "(" ~ ")" |
       "Set" ~ "(" ~ exp ~ ("," ~ exp)* ~ ")" |
       "Multiset" ~ "[" ~ type ~ "]" ~ "(" ~ ")" |
       "Multiset" ~ "(" ~ exp ~ ("," ~ exp)* ~ ")"
}

// seq-constructor-exp ::=
//   "Seq[" type "]()"     | // the empty sequence
//   "Seq(" exp^,* ")"     | // explicit sequence (must not be empty)
//   "[" exp ".." exp ")"    // half-open range of numbers
seq_constructor_exp = {
       "Seq[" ~ type ~ "]()" |
       "Seq(" ~ exp ~ ("," ~ exp)* ~ ")" |
       "[" ~ exp ~ ".." ~ exp ~ ")"
}

// forperm-exp ::=
//     "forperm" "[" ident^,* "]" ident "::" exp
forperm_exp = {
       "forperm" ~ "[" ~ ident ~ ("," ~ ident)* ~ "]" ~ ident ~ "::" ~ exp
}
// let-in-exp ::=
//     "let" ident "==" "(" exp ")" "in" exp
let_in_exp = {
       "let" ~ ident ~ "==" ~ "(" ~ exp ~ ")" ~ "in" ~ exp
}
// magic-wand-exp ::=
//     exp // except that it must not be <==> or ==>
magic_wand_exp = { // TODO: Ban <==> or ==>
       exp
}
// exp ::=
//   "true"  | "false" |          // boolean literal
//   integer |                    // integer literal
//   "null"   |                   // null literal
//   "result" |                   // result literal in
//                                //  function postconditions
//   ident    |                   // local variable read

//   "(" exp ")" |

//   unop exp |                  // unary expression
//   exp binop exp |             // binary expression
//   exp "?" exp ":" exp |       // conditional expression

//   "old" "(" exp ")"           // old expression
//   "[" ident "]" "(" exp ")"   // labeled old expression
//   "lhs" "(" exp ")"           // apply old expression

//   "none"     |             // no permission literal
//   "write"    |             // full permission literal
//   "epsilon"  |             // epsilon permission literal
//   "wildcard" |             // wildcard permission

//   "perm" "(" loc-access ")" | // current permission
//                               //  of given location

//   acc-exp  |                   // accessibility predicate

//   ident "(" exp^,* ")" |                // [domain] function application
//   "(" ident "(" exp^,* ")" ":" type ")" // typed function application

//   field-access         |  // field read
//   predicate-access     |  // predicate access

//   "[" exp "," exp "]" |        // inhale exhale expression
//   "unfolding" acc-exp "in" exp | // unfolding expression
//   "folding" acc-exp "in" exp
//   "applying" ( "(" magic-wand-exp ")" | ident ) "in" exp
//   "packaging" ( "(" magic-wand-exp ")" | ident ) "in" exp

//   // quantification
//   "forall" formal-arg^,* "::" trigger^,* exp |
//   "exists" formal-arg^,* "::" exp |

//   seq-constructor-exp      |
//   set-constructor-exp      |

//   seq-op-exp  |
//   "|" exp "|" |                // length of a sequence; or set/multiset cardinality

//   let-in-exp  |
//   forperm-exp

// TODO(xavier): Fix left recursion in seq ops and field access
// TOD(xavier): Fix precedence parsing of prefix operators
exp_atom = {
       "true" | "false"
      | integer
      | "null"
      | "result"
      | "("~ exp ~ ")"
      | unop~exp
      | "old"~"("~exp~")"
      | "["~ident~"]"~"("~exp~")"
      | "lhs"~"("~exp~")"
      | "none"
      | "write"
      | "epsilon"
      | "wildcard"
      | "perm"~"("~loc_access~")"
      | acc_exp
      | "("~ident~"("~(exp~(","~exp)*)?~")"~":"~type~")"
      // | field_access
       | predicate_access
       | "["~exp~","~exp~"]"
       | "unfolding"~acc_exp~"in"~exp
       | "folding"~acc_exp~"in"~exp
       | "applying"~"("~magic_wand_exp~")"~"in"~exp
       | "packaging"~"("~magic_wand_exp~")"~"in"~exp
       | "forall"~formal_arg~(","~formal_arg)*~"::"~(trigger~(","~trigger)*)?~exp
       | "exists"~formal_arg~(","~formal_arg)*~"::"~exp
       | seq_constructor_exp
       | set_constructor_exp
       // | seq_op_exp
       | "|"~exp~"|"
       | let_in_exp
       | forperm_exp
       | ident~"("~(exp~(","~exp)*)?~")"

       | ident


}
// exp = { exp_atom~(binop_plus~exp_atom)*}
exp = {exp_atom~postfix*~(binop_plus~exp_atom~postfix*)* }
// binop_plus = _{ "+" }
binop_plus = _{ binop | "?"~exp~":" }

postfix = _ {
       | "."~ident
       | "[" ~ seq_op ~ "]"
}
seq_op = {
       ".."~exp | exp ~ ((".." | ":=") ~ exp)?
}


single_exp = _{ SOI~exp~EOI }
// exp = { "true" | "false"
//       | integer
//       | "null"
//       | "result"
//       | ident
//       | "("~ exp ~ ")"
//       | unop~exp
//       | exp~binop~exp
//       | exp~"?"~exp~":"~exp
//       | "old"~"("~exp~")"
//       | "["~ident~"]"~"("~exp~")"
//       | "lhs"~"("~exp~")"
//       | "none"
//       | "write"
//       | "epsilon"
//       | "wildcard"
//       | "perm"~"("~loc_access~")"
//       | acc_exp
//       | ident~"("~exp~(","~exp)*~")"
//       | "("~ident~"("~exp~(","~exp)*~")"~":"~type~")"
// }

// binop ::=
//   "==" | "!=" |                  // equality operators
//   "==>" | "||" | "&&" |"<==>" |  // boolean operators
//   "<" | "<=" | ">" | ">=" |      // ordering
//                                  //  (integers and permissions)
//   "+" | "-" | "*" |              // arithmetic operators
//                                  //  (integers and permissions)
//                                  // also int*permission
//   "\\" | "\%" |                  // arithmetic division
//                                  //  and modulo
//   "\/" |                         // permission division
//                                  //  (of two integers)
//   "--*" |                        // magic wand

//   "union" | "intersection" | "setminus"  //set operators
//   "++"  |           // sequence concatenation
//   "in"  |           // set/multiset/sequence membership
//   "subset"          // subset relation
binop = {
| implies | or | and | iff
| eq | neq
| lte | gte | lt  | gt
| add | sub | mul
| div | rem
| perm_div | magic_wand
| union | intersection | setminus
| concat
| membership
| subset
}
eq = { "==" }
neq = { "!=" }
implies = { "==>" }
or = { "||" }
and = { "&&" }
iff = { "<==>" }
lt = { "<" }
lte = { "<=" }
gt = { ">" }
gte = { ">=" }
add = { "+" }
sub = { "-" }
mul = { "*" }
div = { "\\" }
rem = { "%" }
perm_div = { "/" }
magic_wand = { "--*" }
union = { "union" }
intersection = { "intersection" }
setminus = { "setminus" }
concat = { "++" }
membership = @{ "in"~!(ASCII_ALPHA) }
subset = { "subset" }
// unop ::=
//   "!" |            // boolean negation
//   "+" | "-"        // integer and permission
unop = { "!" | "+" | "-" }

// // Declarations
// sil-program ::=
//   (
//     import    |
//     define    |
//     domain    |
//     field     |
//     function  |
//     predicate |
//     method
//   )*
sil_program = _{ SOI~(declaration)*~EOI }
declaration = {import | define | domain | field | function | predicate | method}

// import ::=
//     "import" relative-path
import = { "import"~relative_path }
// relative-path ::=
//     \A(?:[\w-]+\/?)+\z
// relative_path = { (ident~"/")*~ident }
relative_path = @{ ((ASCII_ALPHANUMERIC | "-")+~"/")*~(ASCII_ALPHANUMERIC | "-")+ ~"/"?}
// define ::=
//     "define" identifier [ "(" parameter^,* ")" ] expression-or-block
define = { "define"~ident~"("~parameter~(","~parameter)*~")"~expression_or_block }

// parameter ::= ident
parameter = { ident }

// expression-or-block ::=
//     exp |
//     block
expression_or_block = { exp | block }

// domain ::=
//   "domain" domain-name "{"
//     domain-function*
//     axiom*
//   "}"
domain = { "domain"~domain_name~"{"~(domain_function)*~(axiom)*~"}" }

// domain-name ::=
//   ident |
//   ident "[" ident^,* "]"  //e.g. Seq[T]
domain_name = { ident~"["~ident~(","~ident)*~"]" | ident }

// domain-function ::=
//   ["unique"] function-signature [";"]
domain_function = { "unique"?~function_signature~";"? }

// function-signature ::=
//   "function" ident formal-args ":" type
function_signature = { "function"~ident~formal_args~":"~type }

// axiom ::=
//   "axiom" ident "{" exp "}" [";"]
axiom = { "axiom"~ident~"{"~exp~"}"~";"? }

// field ::=
//   "field" ident ":" type [";"]
field = { "field"~ident~":"~type~(";"?) }

// function ::=
//   function-signature
//     precondition*
//     postcondition*
//   "{" exp "}" // Semicolon not permitted
function = { function_signature~(precondition)*~(postcondition)*~("{"~exp~"}")? }

// precondition ::=
//   "requires" exp
precondition = { "requires"~exp~";"? }

// postcondition ::=
//   "ensures" exp
postcondition = { "ensures"~exp~";"? }

// invariant ::=
//   "invariant" exp
invariant = { "invariant"~exp }

// predicate ::=
//   "predicate" ident formal-args ["{" exp "}"] // Semicolon not permitted
predicate = { "predicate"~ident~formal_args~("{"~exp~"}")? }

// method ::=
//   "method" ident formal-args [formal-returns]
//     precondition*
//     postcondition*
//   [ block ]
method = { "method"~ident~formal_args~(formal_returns)?~(precondition)*~(postcondition)*~(block)?  }
// formal-args ::=
//   "(" formal-arg^,* ")"
formal_args = { "("~(formal_arg~(","~formal_arg)*)?~")" }

// formal-arg ::=
//   ident ":" type
formal_arg = { ident~":"~type }

// formal-returns ::=
//   "returns" formal-args
formal_returns = { "returns"~formal_args }

// // Statements
// block ::=
//   "{" statement-with-optional-semicolon* "}"
block = { "{"~statement_with_optional_semicolon*~"}" }

// statement-with-optional-semicolon ::=
//   stmt [";"]
statement_with_optional_semicolon = { stmt~";"? }

// stmt ::=
//   // local variable declaration with an optional initial value
//   "var" ident  ":" type [":=" exp] |

//   // local definition
//   define |

//   // local variable assignment
//   ident ":=" exp |

//   // field assignment
//   field-access ":=" exp |

//   // object creation (all fields)
//   ident ":=" "new(*)" |

//   // object creation (specified fields)
//   ident ":=" "new(" ident^,* ")" |

//   "assert" exp |
//   "assume" exp |
//   "inhale" exp |
//   "exhale" exp |
//   "fold"   acc-exp |
//   "unfold" acc-exp |

//   "goto" ident | // goto statement
//   "label ident | // a goto label

//   if-statement           |
//   while-statement        |
//   call-statement         |
//   fresh-statement        |
//   wand-statement         |
//   constraining-block     |

//   // an arbitrary block of statements
//   block

stmt = {
        "var"~ident~":"~type~(":="~exp)? |
       define |
       "assert"~exp |
       "assume"~exp |
       "inhale"~exp |
       "exhale"~exp |
       "fold"~acc_exp |
       "unfold"~acc_exp |
       "goto"~ident |
       "label"~ident |
       ident~":="~exp |
       field_access~":="~exp |
       ident~":="~"new(*)" |
       ident~":="~"new("~ident~(","~ident)*~")" |
       if_statement |
       while_statement |
       call_statement |
       fresh_statement |
       wand_statement |
       constraining_block |
       block
}

single_stmt = { SOI~stmt~EOI }

// if-statement ::=
//   "if" "(" exp ")"
//     block
//   ("elsif" "(" exp ")"
//     block
//   )*            // any number of elseif branches
//   ["else"
//     block
//   ]             // optional else branch
if_statement = { "if"~"("~exp~")"~block~("elsif"~"("~exp~")"~block)*~("else"~block)? }

// while-statement ::=
//   "while" "(" exp ")"
//     invariant*
//     block
while_statement = { "while"~"("~exp~")"~(invariant)*~block }

// call-statement ::= // method call [with return target]
//   [ident^,* :=] ident "(" exp^,* ")"
call_statement = { ((ident~",")*~ident~":=")?~ident~call_statement_args }
call_statement_args = { "("~ ")" | "("~exp~(","~exp)*~")" }

// fresh-statement ::=
//   "fresh" ident^,*
fresh_statement = { "fresh"~ident~(","~ident)* }

// wand-statement ::=
//   "wand" ident ":=" exp |
//   "package" magic-wand-exp |
//   "apply" magic-wand-exp
wand_statement = { "wand"~ident~":"~exp |
       "package"~magic_wand_exp |
       "apply"~magic_wand_exp
}

// constraining-block ::=
//   "constraining" "(" ident^,* ")"
//     block
constraining_block = { "constraining"~"("~ident~(","~ident)*~")"~block }